# User Story 13 - Apagar item

zerarSistema

# entradas válidas

criarUsuario login="mark" nome="Mark Zuckerberg" endereco="Palo Alto, California"
criarUsuario login="steve" nome="Steven Paul Jobs" endereco="Palo Alto, California"
criarUsuario login="bill" nome="William Henry Gates III" endereco="Medina, Washington"

sessaoMark=abrirSessao login="mark"
sessaoSteve=abrirSessao login="steve"
sessaoBill=abrirSessao login="bill"

requisitarAmizade idSessao=${sessaoSteve} login="mark"
requisitarAmizade idSessao=${sessaoBill} login="mark"

aprovarAmizade idSessao=${sessaoMark} login="steve"
aprovarAmizade idSessao=${sessaoMark} login="bill"

idItem1=cadastrarItem idSessao=${sessaoMark} nome="The Social Network" descricao="Filme sobre a fundação da rede social Facebook" categoria="Filme"
idItem2=cadastrarItem idSessao=${sessaoSteve} nome="Guia do mochileiro das galáxias" descricao="Série de ficção científica criada por Douglas Adams" categoria="Livro"
idItem3=cadastrarItem idSessao=${sessaoBill} nome="Final Fantasy XIII" descricao="Décimo terceiro jogo da série produzida pela Square Enix" categoria="Jogo"

apagarItem idSessao=${sessaoSteve} idItem=${idItem2}
expect "O usuário não possui itens cadastrados" getItens idSessao=${sessaoSteve}
expect "O usuário não possui itens cadastrados" getItens idSessao=${sessaoMark} login="steve"
expect "Final Fantasy XIII" pesquisarItem idSessao=${sessaoMark} chave="série" atributo="descricao" tipoOrdenacao="crescente" criterioOrdenacao="dataCriacao"

idRequisicaoEmprestimo1=requisitarEmprestimo idSessao=${sessaoBill} idItem=${idItem1} duracao=7
apagarItem idSessao=${sessaoMark} idItem=${idItem1}
expect "O usuário não possui itens cadastrados" getItens idSessao=${sessaoMark}
expect "O usuário não possui itens cadastrados" getItens idSessao=${sessaoSteve} login="mark"
expect "Nenhum item encontrado" pesquisarItem idSessao=${sessaoSteve} chave="social" atributo="descricao" tipoOrdenacao="crescente" criterioOrdenacao="dataCriacao"

# entradas inválidas

expectError "Sessão inválida" apagarItem idSessao= idItem="xpto"
expectError "Sessão inválida" apagarItem idSessao="" idItem="xpto"
expectError "Sessão inexistente" apagarItem idSessao="xpto" idItem="xpto"

expectError "Identificador do item é inválido" apagarItem idSessao=${sessaoMark} idItem=
expectError "Identificador do item é inválido" apagarItem idSessao=${sessaoMark} idItem=""
expectError "Item inexistente" apagarItem idSessao=${sessaoMark} idItem="xpto"
expectError "Item inexistente" apagarItem idSessao=${sessaoMark} idItem=${idItem1}

expectError "O usuário não tem permissão para apagar este item" apagarItem idSessao=${sessaoMark} idItem=${idItem3}
expectError "Requisição de empréstimo inexistente" aprovarEmprestimo idSessao=${sessaoMark} idRequisicaoEmprestimo=${idRequisicaoEmprestimo1}

idRequisicaoEmprestimo2=requisitarEmprestimo idSessao=${sessaoMark} idItem=${idItem3} duracao=7
idEmprestimo2=aprovarEmprestimo idSessao=${sessaoBill} idRequisicaoEmprestimo=${idRequisicaoEmprestimo2}
expectError "O usuário não pode apagar este item enquanto estiver emprestado" apagarItem idSessao=${sessaoBill} idItem=${idItem3}

encerrarSistema
quit
